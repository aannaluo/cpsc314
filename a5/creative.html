<html>
  <head>
    <meta charset="UTF-8">
    <link rel="shortcut icon" href="#">
    <title>CPSC 314 Assignment 5 Jan 2023</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
<!--    <script src="js/three.js"></script> -->
    <script src="js/three.min.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->
    <script src="js/OBJLoader.js"></script> 
    <script src="js/dat.gui.min.js"></script>   

<script id="floorVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="myVertShader" type="x-shader/x-vertex">
uniform vec2 uvScale;
varying vec2 vUv;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main()
{       
  vUv = uvScale * uv;
  ocsPosition = position;
  vec4 vcsP = modelViewMatrix * vec4(ocsPosition,1.0);
  vcsPosition = vcsP.xyz;
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vcsNormal = normalMatrix * normal;       // normalMatrix is the transpose-inv of the rotation matrix
}
</script>

<script id="skyBoxVertShader" type="x-shader/x-vertex">
varying vec2 vUv;
void main()
{       
  vec4 vcsP = modelViewMatrix * vec4(position,1.0);
  gl_Position = projectionMatrix * vcsP;   // pass on point in CCS
  vUv = uv;
}
</script>

<script id="holeyFragShader" type="x-shader/x-fragment">
uniform vec3 vcsLightPosition;
uniform vec3 myColor;
varying vec3 vcsNormal;
varying vec3 vcsPosition;
varying vec3 ocsPosition;

void main( void ) {
  float x = ocsPosition.x * 5.0;
  float y = ocsPosition.y * 5.0;
  float z = ocsPosition.z * 5.0;
  float dx = x - floor(x+0.5);
  float dy = y - floor(y+0.5);
  float dz = z - floor(z+0.5);
  float r2 = dx*dx+dy*dy+dz*dz;
  vec3 color = vec3(0,1,1);
  if (r2 < 0.15) {
    discard;
  } else {
    gl_FragColor = vec4(color,1.0);
  }
  
}
</script>

<script id="toonFragShader" type="x-shader/x-fragment">
varying vec3 vcsNormal;
varying vec3 vcsPosition;
uniform vec3 vcsLightPosition;
uniform vec3 toonColor;

void main( void ) {
  vec3 lightNormal = vec3(vcsLightPosition - vcsPosition);
  vec3 N = normalize(vcsNormal);
  vec3 L = normalize(lightNormal);
  float i = dot(N, L);
  i = ceil(i * 8.0);
 if (i > 4.0) {
    i -= 5.0;
    i = i / 4.0;
  } else if (i == 0.0) {
    i = 0.0;
  } else {
    i = i/8.0 + 0.2;
  }
  gl_FragColor = vec4(i * toonColor, 1.0);   // default is yellow
}
</script>

<script id="waveVertShader" type="x-shader/x-fragment">
  uniform float u_time;
  uniform float clock_time;
  uniform float u_pointsize;
  uniform float u_noise_amp_1;
  uniform float u_noise_freq_1;
  uniform float u_spd_modifier_1;
  uniform float u_noise_amp_2;
  uniform float u_noise_freq_2;
  uniform float u_spd_modifier_2;
  // 2D Random
  float random (in vec2 st) {
      return fract(sin(dot(st.xy,
                          vec2(12.9898,78.233)))
                  * 43758.5453123);
  }
  // 2D Noise based on Morgan McGuire @morgan3d
  // https://www.shadertoy.com/view/4dS3Wd
  float noise (in vec2 st) {
      vec2 i = floor(st);
      vec2 f = fract(st);
      // Four corners in 2D of a tile
      float a = random(i);
      float b = random(i + vec2(1.0, 0.0));
      float c = random(i + vec2(0.0, 1.0));
      float d = random(i + vec2(1.0, 1.0));
      // Smooth Interpolation
      // Cubic Hermine Curve.  Same as SmoothStep()
      vec2 u = f*f*(3.0-2.0*f);
      // u = smoothstep(0.,1.,f);
      // Mix 4 coorners percentages
      return mix(a, b, u.x) +
              (c - a)* u.y * (1.0 - u.x) +
              (d - b) * u.x * u.y;
  }
  mat2 rotate2d(float angle){
      return mat2(cos(angle),-sin(angle),
                sin(angle),cos(angle));
  }
  void main() {
    gl_PointSize = u_pointsize;
    vec3 pos = position;
    // pos.xy is the original 2D dimension of the plane coordinates
    pos.z += noise(pos.xy * u_noise_freq_1 + clock_time * u_spd_modifier_1) * u_noise_amp_1;
    // add noise layering
    // minus clock_time makes the second layer of wave goes the other direction
    pos.z += noise(rotate2d(3.1416 / 4.) * pos.yx * u_noise_freq_2 - clock_time * u_spd_modifier_2 * 0.6) * u_noise_amp_2;
    vec4 mvm = modelViewMatrix * vec4(pos, 1.0);
    gl_Position = projectionMatrix * mvm;
  }
  </script>

  <script id="waveFragShader" type="x-shader/x-fragment">
    //precision mediump float;
    
    uniform vec2 u_resolution;
    void main() {
      vec2 st = gl_FragCoord.xy/u_resolution.xy;
      gl_FragColor = vec4(vec3(0.0, st),1.0);
    }
  </script>


<script id="envmapFragShader" type="x-shader/x-fragment">
uniform sampler2D uPosxTexture;
uniform sampler2D uPosyTexture;
uniform sampler2D uPoszTexture;
uniform sampler2D uNegxTexture;
uniform sampler2D uNegyTexture;
uniform sampler2D uNegzTexture;
uniform vec3 myColor;
uniform mat4 matrixWorld;
uniform vec3 vcsLightPosition;
varying vec3 vcsNormal;
varying vec3 ocsPosition;
varying vec3 vcsPosition;

void main( void ) {
  vec3 N = normalize(vcsNormal);
  vec3 I = normalize(vcsPosition);
  vec3 R = reflect(I, N);
  vec3 color;
  vec4 R_world = matrixWorld*vec4(R, 0.0);

  vec3 R4 = vec3(R_world.x, R_world.y, R_world.z);

  float u;
  float v;
  
  if (R4.y >= abs(R4.x) && R4.y >= abs(R4.z)) {
    u = ((-R4.x/R4.y) + 1.0) / 2.0;
    v = ((-R4.z/R4.y) + 1.0) / 2.0;
    color = texture2D(uPosyTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color,1.0);
  } else if (R4.x >= abs(R4.y) && R4.x >= abs(R4.z)) {
    u = ((R4.z/R4.x) + 1.0) / 2.0;
    v = ((R4.y/R4.x) + 1.0) / 2.0;
    color = texture2D(uPosxTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color,1.0);
  } else if (R4.z >= abs(R4.y) && R4.z >= abs(R4.x)) {
    u = ((-R4.x/R4.z) + 1.0) / 2.0;
    v = ((R4.y/R4.z) + 1.0) / 2.0;
    color = texture2D(uPoszTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  } else if (R4.y <= -abs(R4.x) && R4.y <= -abs(R4.z)) {
    u = ((R4.x/R4.y) + 1.0) / 2.0;
    v = ((-R4.z/R4.y) + 1.0) / 2.0;
    color = texture2D(uNegyTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color,1.0);
  } else if (R4.x <= -abs(R4.y) && R4.x <= -abs(R4.z)) { 
    u = ((R4.z/R4.x) + 1.0) / 2.0;
    v = ((-R4.y/R4.x) + 1.0) / 2.0;
    color = texture2D(uNegxTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color,1.0);
  } else if (R4.z <= -abs(R4.y) && R4.z <= -abs(R4.x)) {
    u = ((-R4.x/R4.z) + 1.0) / 2.0;
    v = ((-R4.y/R4.z) + 1.0) / 2.0;
   color = texture2D(uNegzTexture, vec2(u,v)).xyz;
    gl_FragColor = vec4(color, 1.0);
  } else { 
    gl_FragColor = vec4(0.1,0.2,0.7,1.0);
  }
}
</script>

<script id="floorFragShader" type="x-shader/x-fragment">
uniform vec3 vcsLightPosition;
uniform vec3 myColor;
varying vec3 vcsPosition;
varying vec3 ocsPosition;
uniform sampler2D normalMap;
uniform sampler2D textureMap;
varying vec2 vUv;


void main( void ) {
  vec3 normalTex = texture2D(normalMap, vUv).xyz;
  normalTex = 2.0 * normalTex - 1.0;

  vec3 color = texture2D(textureMap, vUv).xyz;
  vec3 N = vec3(normalTex.x,normalTex.z,-normalTex.y);
  vec3 L = normalize(vec3(vcsLightPosition - vcsPosition));
  float light = dot(N, L);

  color = color*light;
  gl_FragColor = vec4(color,1.0);
}
</script>


<script id="pnoiseFragShader" type="x-shader/x-fragment">
float screenWidth = 2.0;
float PI = 3.1416;

// perlin noise code from: 
//   https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83

float rand(vec2 c){
	return fract(sin(dot(c.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float noise(vec2 p, float freq ){
	float unit = screenWidth/freq;
	vec2 ij = floor(p/unit);
	vec2 xy = mod(p,unit)/unit;
	//xy = 3.*xy*xy-2.*xy*xy*xy;
	xy = .5*(1.-cos(PI*xy));
	float a = rand((ij+vec2(0.,0.)));
	float b = rand((ij+vec2(1.,0.)));
	float c = rand((ij+vec2(0.,1.)));
	float d = rand((ij+vec2(1.,1.)));
	float x1 = mix(a, b, xy.x);
	float x2 = mix(c, d, xy.x);
	return mix(x1, x2, xy.y);
}

float pNoise(vec2 p, int res){
	float persistance = .5;
	float n = 0.;
	float normK = 0.;
	float f = 4.;
	float amp = 1.;
	int iCount = 0;
	for (int i = 0; i<50; i++){
		n+=amp*noise(p, f);
		f*=2.;
		normK+=amp;
		amp*=persistance;
		if (iCount == res) break;
		iCount++;
	}
	float nf = n/normK;
	return nf*nf*nf*nf;
}

varying vec3 ocsPosition;
varying vec3 vcsPosition;
void main( void ) {
  int levels = 100;
  float i=pNoise(vcsPosition.xy,levels);
  float j=floor(i+0.9); 
  gl_FragColor = vec4(j,j,0.3,1.0);
  //gl_FragColor = vec4(i,i,i,1.0);
}
</script>

<script src="motion.js"></script>
<script src="creative.js"></script>
</body>
</html>

